//===- use_def_analysis.dl --------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This modules computes def-uses chains for a subset of the registers (defined in track_register).
and only if those chains are related eventually to an access to memory.

-'def' computes definitions of registers at each of the locations.

-'def_used_for_address' compute the set of live uses backwards at each of the locations.
 However, it only computes uses starting at accesses to memory.

-'def_used' intersects the two previous facts to obtain def-use chains.

-'straight_line_def_used` is a weaker version of 'def_used' that can be used before
code inference.

The traversal of def_used_for_address is kept intra-procedural (not following call edges).
*/


// register is defined in this location
.decl def(EA:address,Reg:reg_group)

/**
Instruction at address 'EA' uses register 'Reg' at operand index 'Index'.
*/
.decl used(EA:address,Reg:reg_group,Index:operand_index)

//The 'reg' is defined in 'ea_def' and used in 'ea_used' in the operand with index 'index_used'
.decl def_used(ea_def:address,reg:reg_group,ea_used:address,index_used:operand_index)
.output def_used

.decl def_used_intra(ea_def:address,reg:reg_group,ea_used:address,index_used:operand_index)
.output def_used_intra

// a register is implicitly defined by being compared to a constant and then jumping
// this definition only takes place in between the jump and the target that implies equality
.decl flow_def(EA:address,Reg:reg_group,EA_next:address,Value:number)
//////////////////////////////////////////////////////////////////////////////
//local underapproximation of control flow
.decl local_next(ea:address,ea_next:address) inline

local_next(EA,EA_next):-
    code_in_block(EA,Block),
    may_fallthrough(EA,EA_next),
    code_in_block(EA_next,Block).

.decl inter_procedural_jump(Src:address,Dest:address)

inter_procedural_jump(Src,Dest):-
    unconditional_jump(Src),
    direct_jump(Src,Dest),
    direct_call(OtherSrc,Dest),
    code(OtherSrc).

.decl block_next(Block:address,BlockEnd:address,NextBlock:address)
.output block_next

block_next(Block,EA,Block2):-
    block_last_instruction(Block,EA),
    may_fallthrough(EA,Block2),
    !no_return_call_refined(EA),
    block(Block2).

block_next(Block,EA,EA_next):-
    block_last_instruction(Block,EA),
    !arch.delay_slot(_,EA),
    direct_jump(EA,EA_next),
    !inter_procedural_jump(EA,EA_next).

block_next(Block,EA,EA_next):-
    block_last_instruction(Block,EA),
    arch.delay_slot(JumpEA,EA),
    direct_jump(JumpEA,EA_next),
    !inter_procedural_jump(JumpEA,EA_next).

block_next(Block,EA,Dest):-
    block_last_instruction(Block,EA),
    jump_table_target(EA,Dest).

// Path created by exception handling.
block_next(Block,EA,Block2):-
    lsda_callsite_addresses(Beg,End, Block2),
    block_last_instruction(Block,EA),
    EA >= Beg, EA < End,
    block(Block2).

//////////////////////////////////////////////////////////////////////////////
// Main definitions

// Define any registers defined by the instruction
def(EA,Reg):-
    arch.defines_reg(EA,Reg).

// A call "defines" all caller-saved registers
def(EA,Reg):-
    arch.call(EA,_),
    !arch.delay_slot(EA,_),
    arch.function_non_maintained_reg(Reg).

// A call "defines" all caller-saved registers (variant for delay slots)
def(EA,Reg):-
    arch.call(Call,_),
    arch.delay_slot(Call,EA),
    arch.function_non_maintained_reg(Reg).

def(EA,TrackedReg):-
    direct_call(EA,EA_pc_thunk),
    get_pc_thunk(EA_pc_thunk,Reg),
    arch.track_register(Reg,TrackedReg).

flow_def(EA_jump,Reg,EA_target,Immediate):-
    code(EA),
    cmp_immediate_to_reg(EA,Reg,_,Immediate),
    next(EA,EA_jump),
    direct_jump(EA_jump,EA_target),
    arch.jump(EA_jump),
    arch.conditional(EA_jump,"E").

flow_def(EA_jump,Reg,EA_target,Immediate):-
    code(EA),
    cmp_immediate_to_reg(EA,Reg,_,Immediate),
    next(EA,EA_jump),
    direct_jump(EA_jump,_),
    next(EA_jump,EA_target),
    arch.jump(EA_jump),
    arch.conditional(EA_jump,"NE").

.decl must_def(EA:address,Reg:reg_group) inline

must_def(EA,Reg):-
    def(EA,Reg),
    !arch.conditional(EA,_).

.decl defined_in_block(EA:address,Reg:reg_group)

defined_in_block(Block,Reg):-
    def(EA,Reg),
    code_in_block(EA,Block).

/**
The register is explicitly used in an operand.
*/
.decl used_explicit(EA:address,Reg:reg_group,Index:operand_index)

used_explicit(EA,Reg,Index):-
    instruction_get_src_op(EA,Index,Op),
    op_regdirect_contains_reg(Op,Reg),
    !is_xor_reset(EA).

used_explicit(EA,Reg,Index):-
    instruction_get_op(EA,Index,Op),
    op_indirect_contains_reg(Op,Reg).

used(EA,Reg,Index):-
    used_explicit(EA,Reg,Index).

// Implicitly-used registers use an operand index of zero.
used(EA,Reg,0):-
    arch.uses_reg(EA,Reg),
    !used_explicit(EA,Reg,_).

.decl used_in_block(EA:address,Reg:reg_group)

used_in_block(Block,Reg):-
    used(EA,Reg,_),
    code_in_block(EA,Block).

// the last address where a register was defined within the block
.decl block_last_def(EA:address,EA_def:address,Reg:reg_group)



block_last_def(EA_next,EA,Reg):-
    def(EA,Reg),
    local_next(EA,EA_next).

block_last_def(EA_next,EA_def,Reg):-
    block_last_def(EA,EA_def,Reg),
    !must_def(EA,Reg),
    local_next(EA,EA_next).

// the last address where a register was defined at the basic block level.
.decl last_def(EA:address,EA_def:address,Reg:reg_group)

last_def(Block,EA,Reg):-
    def(EA,Reg),
    block_next(_,EA,Block),
    !flow_def(EA,Reg,Block,_).

last_def(Block,EA_def,Reg):-
    block_last_def(Block_end,EA_def,Reg),
    !must_def(Block_end,Reg),
    block_next(_,Block_end,Block),
    !flow_def(Block_end,Reg,Block,_).

last_def(Block_next,EA_def,Reg):-
    last_def(Block,EA_def,Reg),
    !defined_in_block(Block,Reg),
    block_next(Block,Block_end,Block_next),
    !flow_def(Block_end,Reg,Block,_).

last_def(Block_next,Block_end,Reg):-
    flow_def(Block_end,Reg,Block_next,_).


def_used_intra(EA_def,Reg,EA_used,Index):-
    used(EA_used,Reg,Index),
    block_last_def(EA_used,EA_def,Reg).


def_used_intra(EA_def,Reg,EA_used,Index):-
    last_def(Block,EA_def,Reg),
    code_in_block(EA_used,Block),
    !block_last_def(EA_used,_,Reg),
    used(EA_used,Reg,Index).

def_used(EA_def,Reg,EA_used,Index):-
    def_used_intra(EA_def,Reg,EA_used,Index).

def_used(EA_def,Reg,EA_used,Index_used):-
    def_used_return_val_reg(EA_def,_,Reg,EA_used,Index_used).

/**
A value returned from a function is used.
*/
.decl return_val_used(EA_call:address,Callee:address,Reg:reg_group,EA_used:address,Index_used:operand_index)

return_val_used(EA_call,Callee,Reg,EA_used,Index_used):-
    arch.return_val_reg(Reg),
    def_used_intra(EA_call,Reg,EA_used,Index_used),
    direct_call(EA_call,Callee).

/**
A value is returned from a called procedure Callee, from the last Block with
the last instruction at BlockEnd.
*/
.decl return_block_end(Callee:address,CalleeEnd:address,Block:address,BlockEnd:address)

return_block_end(Callee,CalleeEnd,Block,BlockEnd):-
    direct_call(_,Callee),
    fde_addresses(Callee,CalleeEnd),
    arch.return(CalleeReturn),
    Callee <= CalleeReturn,
    CalleeReturn < CalleeEnd,
    code_in_block(CalleeReturn,Block),
    block_last_instruction(Block,BlockEnd).

/**
A register is defined and then returned from a called function
*/
.decl return_val_def(Callee:address,Reg:reg_group,EA_def:address)

// Simple case: the returning block defines the return register.
return_val_def(Callee,Reg,EA_def):-
    arch.return_val_reg(Reg),
    return_block_end(Callee,_,_,BlockEnd),
    block_last_def(BlockEnd,EA_def,Reg).

// The return register is defined elsewhere in the function.
return_val_def(Callee,Reg,EA_def):-
    last_def(Block,EA_def,Reg),
    arch.return_val_reg(Reg),
    return_block_end(Callee,CalleeEnd,Block,BlockEnd),
    !block_last_def(BlockEnd,_,Reg),
    Callee <= EA_def,
    EA_def < CalleeEnd.

/**
A function defines a return value and it is used by the caller.

reg: return-value register
ea_used: use of the return-value register after a call
ea_call: the corresponding call
ea_def: def of the return-value register in the callee
*/
.decl def_used_return_val_reg(ea_def:address,ea_call:address,reg:reg_group,ea_used:address,index_used:operand_index)
.output def_used_return_val_reg

def_used_return_val_reg(EA_def,EA_call,Reg,EA_used,Index_used):-
    return_val_used(EA_call,Callee,Reg,EA_used,Index_used),
    return_val_def(Callee,Reg,EA_def).

/**
 * The register 'Reg' used as an address at address 'EA'.
 */
.decl used_for_address(EA:address,Reg:reg_group)
.output used_for_address

used_for_address(EA,Reg):-
    used(EA,Reg,Index),
    instruction_get_op(EA,Index,Op),
    op_indirect_contains_reg(Op,Reg).

used_for_address(EA,RegGroup):-
    reg_jump(EA,Reg),
    arch.track_register(Reg,RegGroup),
    used(EA,RegGroup,_).

used_for_address(EA,RegGroup):-
    reg_call(EA,Reg),
    arch.track_register(Reg,RegGroup),
    used(EA,RegGroup,_).

/**
 * The register 'Reg' as defined at address 'EA_def' is later used either as an address
 * or to compute an address.
 */
.decl def_used_for_address(EA_def:address,Reg:reg_group)
.output def_used_for_address

def_used_for_address(EA_def,Reg):-
    def_used(EA_def,Reg,EA,_),
    used_for_address(EA,Reg).

def_used_for_address(EA_def,Reg):-
    def_used_for_address(EA_used,_),
    def_used(EA_def,Reg,EA_used,_).

def_used_for_address(EA,Reg):-
    arch.pc_relative_addr(EA,Reg,_).
