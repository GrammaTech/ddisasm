//===- arm_binaries.dl --------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019-2022 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
Define a set predicates to abstract from ARM specific instruction set
and characteristics
*/

avoid_symbols("$d").

block_points(Block,"data",0,9,"literal pool"),
data_block_candidate(Block,Size):-
    defined_symbol(Block,Size,_,_,_,_,_,_,"$d").

block_points(Block,"code",0,3,"plausible instruction sequence: cmp/conditional-instruction"):-
    (
        conditional_jump(EA)
        ;
        conditional_return(EA)
    ),
    code_in_block_candidate(EA,Block),
    next(PrevEA,EA),
    code_in_block_candidate(PrevEA,Block),
    instruction_get_operation(PrevEA,Operation),
    arch.cmp_operation(Operation).

plt_entry(EA,Function):-
    plt_section(SecName),
    loaded_section(Beg,End,SecName),
    EA >= Beg,
    EA < End,
    arch.jump(EA),
    instruction_get_op(EA,_,Op),
    op_indirect(Op,_, _, _,_, Offset, _),
    Got_entry = as(as(65536+ EA,number) + Offset, address),
    relocation(Got_entry,_,Function,_,_,_,_).


symbolic_operand_point(EA,Imm_index,-2,"invalid symbolic operand for mov"):-
    symbolic_operand_candidate(EA,Imm_index,_,_),
    // If no attribute is found, the immediate is not likely symbolic.
    // Note that lower16 or upper16 is not allowed for mov.
    // However, upper8_15 seems valid: e.g., MOVS r0, #:upper8_15:#foo
    !symbolic_operand_attribute(EA,_,_),
    instruction_get_operation(EA,Operation),
    Operation != "MOVT", Operation != "MOVW", contains("MOV",Operation).

symbolic_operand_point(EA,Imm_index,-1,"unlikely to have symbolic operand"):-
    symbolic_operand_candidate(EA,Imm_index,_,_),
    instruction_get_operation(EA,Operation),
    (
        arch.cmp_operation(Operation)
        ;
        arch.logic_operation(Operation)
    ).

symbolic_operand_point(EA,2,-2,"arm: add r, r, n"):-
    instruction(EA,_,_,Operation,Op1,Op2,Op3,0,_,_),
    (
        substr(Operation,0,3) = "ADD";
        substr(Operation,0,3) = "SUB";
        substr(Operation,0,3) = "RSB"
    ),
    op_regdirect_contains_reg(Op1,Reg),
    !arch.pc_reg(Reg),
    op_immediate(Op2,_),
    Op3 != 0,
    !symbolic_operand_attribute(EA,_,_).

unlikely_have_symbolic_immediate(EA):-
    instruction_get_operation(EA, Operation),
    (
        // NOTE: This is not exhaustive.
        arch.logic_operation(Operation);
        Operation != "MOVT", Operation != "MOVW", contains("MOV",Operation);
        contains("CMP", Operation);
        contains("CMN", Operation);
        contains("SUB", Operation);
        contains("RSB", Operation)
    ).

instruction_has_relocation(EA,EA-Mode):-
    binary_type("REL"),
    instruction(EA,_,_,_,_,_,_,_,_,_),
    Mode = EA band 1,
    relocation(EA-Mode,_,_,_,_,_,_).

symbolic_expr_from_relocation(EA,4,Symbol,Addend,Dest):-
    binary_type("REL"),
    relocation(EA,"ABS32",Symbol,_,SymbolIndex,_,"REL"),
    symbol(Base,_,_,_,_,_,_,SymbolIndex,Symbol),
    data_word(EA,4,Value),
    // (S + A) | T
    S = as(Base,unsigned),
    A = as(Value,unsigned),
    T = as(Base band 1,unsigned),
    Dest = ((S + A) bor T),
    Addend = as(Base - Dest,number).

symbolic_expr_attribute(EA,"GotRelPC"),
symbolic_expr_from_relocation(EA,4,Symbol,Addend,EA+as(Addend,address)):-
    binary_type("REL"),
    relocation(EA,"GOT_BREL",Symbol,_,_,_,_),
    data_word(EA,4,Addend).


/**
Map ARM condition suffixes to standard condition codes
*/
.decl condition_code_map(Suffix:symbol,CC:condition_code)

condition_code_map("EQ","E").
condition_code_map("NE","NE").
condition_code_map("GT","G").
condition_code_map("LT","L").
condition_code_map("GE","GE").
condition_code_map("LE","LE").
condition_code_map("HS","AE").
condition_code_map("LO","B").
condition_code_map("MI","S").
condition_code_map("PL","NS").
condition_code_map("VS","O").
condition_code_map("VC","OV").
condition_code_map("HI","A").
condition_code_map("LS","BE").
