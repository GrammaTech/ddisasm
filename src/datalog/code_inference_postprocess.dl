//===- code_inference_postprocess.dl ------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//

/*
  After symbolization, we split blocks if there are symbols pointing to
  the middle of the block and then merge blocks that start at addresses
  that are never referenced and must fallthrough from another block.
  Compute padding regions.

*/

.decl code_in_refined_block(ea:address,block:address)
.output code_in_refined_block

.decl refined_block(block:address)
.output refined_block

.decl block_information(Block:address,Size:unsigned,StartAddress:address,EndAddress:address)
.output block_information

// Last instruction of refined block:
//   In existence of branch with a delay-slot, the delay-slot is the last
//   instruction.
.decl refined_block_last_instruction(block:address,EA:address)
// Last instruction (at runtime) of refined block:
//   In existence of branch with a delay-slot, the branch is the last
//   instruction.
.decl refined_block_control_instruction(block:address,EA:address)

////////////////////////////////////////////////////////
// First we split blocks if there is a symbol pointing to the middle

.decl block_needs_splitting_at(EA:address)
.decl code_in_split_block(ea:address,block:address)
.decl split_block(block:address)

block_needs_splitting_at(EA):-
    // only these sym exprs should be able to point to the middle of a block.
    // other kinds of symbols we considered as potential block limits during
    // code inference.
    (
        symbol_minus_symbol(_,_,_,EA,_,_);
        symbol_minus_symbol(_,_,EA,_,_,_)
    ),
    code_in_block(EA,Block),
    EA != Block.

block_needs_splitting_at(EA):-
    binary_isa("MIPS"),
    // When a block consists of some nop paddings followed by code,
    // the block needs to be split at the first code EA after the nop paddings.
    padding_prefix_end(EA,_).

// For jump table entry targets: to split the target blocks
block_needs_splitting_at(TargetAddr):-
    jump_table_candidate_refined(_,_,TargetAddr).

split_block(Block):-
    block(Block).
split_block(EA):-
    block_needs_splitting_at(EA).

code_in_split_block(Block,Block):-
    split_block(Block).

code_in_split_block(EA,Block):-
    code_in_split_block(PrevEA,Block),
    code_in_block(PrevEA,PrevBlock),
    next(PrevEA,EA),
    code_in_block(EA,PrevBlock),
    !block_needs_splitting_at(EA).

////////////////////////////////////////////////////////
// Once we have split blocks, we merge the ones that can be merged.

// a block that is not referenced anywhere
.decl block_needs_merging(block:address)

block_needs_merging(Block):-
    split_block(Block),
    must_fallthrough(Code,Block),
    code(Code),
    //not referenced anywhere
    !symbolic_operand(_,_,Block,_),
    !code_pointer_in_data(_,Block),
    !symbol_minus_symbol(_,_,_,Block,_,_),
    !symbol_minus_symbol(_,_,Block,_,_,_),
    !moved_data_label(_,_,_,Block),
    !moved_label(_,_,_,Block),
    !function_symbol(Block,_),
    !defined_symbol(Block,_,_,_,_,_,_,_,_),
    !entry_point(Block),
    !dynamic_entry("INIT",Block),
    !dynamic_entry("FINI",Block),
    !instruction_has_loop_prefix(Block),
    !fde_addresses(Block,_),
    !fde_addresses(_,Block),
    !block_needs_splitting_at(Block),
    !loaded_section(_,Block,_).

refined_block(Block):-
    split_block(Block),
    !block_needs_merging(Block).

code_in_refined_block(EA,Block):-
    refined_block(Block),
    code_in_split_block(EA,Block).

code_in_refined_block(EA,Block):-
    block_needs_merging(OldBlock),
    code_in_split_block(EA,OldBlock),
    must_fallthrough(Prev,OldBlock),
    code_in_refined_block(Prev,Block).

////////////////////////////////////////////////////////
// Finally, we compute additional information about the new blocks.

refined_block_last_instruction(Block,EA):-
    code_in_refined_block(EA,Block),
    next(EA,Next),
    !code_in_refined_block(Next,Block).

refined_block_control_instruction(Block,EA):-
    refined_block_last_instruction(Block,EA),
    !arch.delay_slot(EA,_).

refined_block_control_instruction(Block,Branch):-
    refined_block_last_instruction(Block,EA),
    arch.delay_slot(Branch,EA).

block_information(Block,BlockSize,FirstEA,FirstEA+BlockSize):-
    refined_block_last_instruction(Block,LastInst),
    arch.instruction_at(FirstEA,Block),
    instruction(LastInst,InsnSize,_,_,_,_,_,_,_,_),
    BlockSize = LastInst + InsnSize - Block.

/**
Blocks are either contiguous or separated by data segments
This assumes there are no remaining overlapping blocks.
This is similar to next_block_in_byte_interval, but this uses the refined
block information (i.e., block_information).
*/
.decl next_refined_block_in_byte_interval(Block:address, NextBlock:address)

next_refined_block_in_byte_interval(Block,NextBlock):-
    block_information(Block,_,_,End),
    block_information(NextBlock,_,End,_).

next_refined_block_in_byte_interval(Block,NextBlock):-
    block_information(Block,_,_,End),
    data_in_code(End,DataEnd),
    block_information(NextBlock,_,DataEnd,_).

////////////////////////////////////////////////////////
// Compute padding regions

.decl padding(EA:address,Size:unsigned)
.output padding

// blocks with only nops are padding
padding(EA,Size):-
    nop_block(EA),
    block_information(EA,Size,_,_).

// padding that does not form a block
padding(EA,Size):-
    after_end(NonNop,End),
    //this condition guarantees that we take the last after_end of a sequence of nops
    !is_padding(NonNop),
    next(End,EA),
    is_padding(EA),
    Size = NonNop-EA,
    Size > 0.

.decl block_has_non_nop(Block:address)

block_has_non_nop(Block):-
    code_in_refined_block(EA,Block),
    !arch.is_nop(EA).

.decl nop_block(Block:address)

nop_block(Block):-
    refined_block(Block),
    !block_has_non_nop(Block).

// nops before code in a block
.decl padding_prefix(EA:address,Block:address)

padding_prefix(EA,Block):-
    code_in_block(EA,Block),
    arch.is_nop(EA),
    EA = Block.

padding_prefix(EA,Block):-
    padding_prefix(PrevEA,Block),
    next(PrevEA,EA),
    code_in_block(EA,Block),
    arch.is_nop(EA).

// the first code EA after nop pads in the given block
.decl padding_prefix_end(EA:address,Block:address)

padding_prefix_end(EA,Block):-
    code_in_block(EA,Block),
    !arch.is_nop(EA),
    next(PrevEA,EA),
    padding_prefix(PrevEA,Block).
