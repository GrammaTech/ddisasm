//===- elf_binaries.dl --------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//

#include "exceptions.dl"
#include "relocations.dl"

// This module defines predicates and rules specific of ELF binaries

.decl elf_section_type(Name:symbol,Code:unsigned)

elf_section_type("NULL",0).
elf_section_type("PROGBITS",1).
elf_section_type("SYMTAB",2).
elf_section_type("STRTAB",3).
elf_section_type("RELA",4).
elf_section_type("HASH",5).
elf_section_type("DYNAMIC",6).
elf_section_type("NOTE",7).
elf_section_type("NOBITS",8).
elf_section_type("REL",9).
elf_section_type("SHLIB",10).
elf_section_type("DYNSYM",11).

elf_section_type("INIT_ARRAY",14).
elf_section_type("FINI_ARRAY",15).
elf_section_type("PREINIT_ARRAY",16).
elf_section_type("GROUP",17).
elf_section_type("SYMTAB_SHNDX",18).
elf_section_type("NUM",19).

data_section(Name):-
    section_property(Name,"Loaded"),
    !section_property(Name,"Executable"),
    section_type(Name,Type),
    (   elf_section_type("PROGBITS",Type);
        elf_section_type("NOBITS",Type);
        elf_section_type("INIT_ARRAY",Type);
        elf_section_type("FINI_ARRAY",Type);
        elf_section_type("PREINIT_ARRAY",Type);
        elf_section_type("DYNAMIC",Type);
        elf_section_type("RELA",Type)
    ).

exception_section(".eh_frame").
exception_section(".eh_frame_hdr").
exception_section(".gcc_except_table").

special_data_section(Section):-
    exception_section(Section).

special_data_section(".dynamic").
special_data_section(".fini_array").
special_data_section(".got").
special_data_section(".got.plt").
special_data_section(".init_array").
special_data_section(".interp").
special_data_section(".jcr").
special_data_section(".tm_clone_table").

function_pointer_section(".init_array"):-binary_format("ELF").
function_pointer_section(".fini_array"):-binary_format("ELF").
function_pointer_section(".got.plt"):-binary_format("ELF").

////////////////////////////////////////////////////////////////////////////
// detect references  to libraries (calls to plt)
.decl plt_section(name:symbol)

plt_section(".plt").
plt_section(".plt.got").
plt_section(".plt.sec").

.decl got_section(name:symbol)

got_section(".got").
got_section(".got.plt").

bss_section(Name):-
    binary_format("ELF"),
    section_property(Name,"Loaded"),
    !section_property(Name,"Initialized").

non_zero_data_section(Name):-
    data_section(Name),
    !bss_section(Name),
    Name != ".dynamic".

////////////////////////////////////////////////////////////////////////////
// detect references  to libraries (calls to plt)
.decl plt_entry(ea:address, function:symbol)

plt_entry(EA,Function):-
    plt_section(SecName),
    loaded_section(Beg,End,SecName),
    EA >= Beg,
    EA < End,
    pc_relative_jump(EA,Got_entry),
    relocation(Got_entry,_,Function,_,_,_,_),
    Function != "".

plt_entry(EA,Name):-
    plt_section(SecName),
    loaded_section(Beg,End,SecName),
    EA >= Beg,
    EA < End,
    pc_relative_jump(EA,Got_entry),
    relocation(Got_entry,"IRELATIVE","",Dest,_,_,_),
    (
        defined_symbol(as(Dest,address),_,"GNU_IFUNC",_,_,_,_,_,Name);
        !defined_symbol(as(Dest,address),_,"GNU_IFUNC",_,_,_,_,_,_),
        ifunc_symbol(as(Dest,address),Name)
    ).

plt_block(Block,Function):-
    plt_entry(EA,Function),
    code_in_block(EA,Block).

// If a block in the PLT section must fallthrough to another block in the
// PLT, consider it to be a PLT block for Function.
plt_block(Block,Function):-
    plt_block(NextBlock,Function),
    must_fallthrough(EA,NextBlock),
    code_in_block(EA,Block),
    plt_section(SecName),
    loaded_section(Beg,End,SecName),
    Block >= Beg,
    EA < End.

.decl plt_data_reference(ea:address)

plt_data_reference(EA):-
    plt_block(Dest,_),
    symbolic_data(EA,_,Dest).

//////////////////////////////////////////////////////////////////////////

/**
Value of register 'Reg' at address 'EA' is a GOT-relative base reference.
*/
.decl reg_has_got(EA:address,Reg:register)
.output reg_has_got

/**
Address 'EA' is used as a GOT-relative base reference. 'EA' is the beginning of
either the .got or .got.plt section.
*/
.decl got_reference_pointer(EA:address)
.output got_reference_pointer

got_reference_pointer(EA):-
    loaded_section(EA,_,".got.plt").

got_reference_pointer(EA):-
    loaded_section(EA,_,".got"),
    !loaded_section(_,_,".got.plt").

// Identify load PC operations:
// e.g. (clang)
//    call  .L1$pb
// .L1$pb:
//    pop  REG
// .Ltmp0:
//    add  REG, offset _GLOBAL_OFFSET_TABLE_+(.Ltmp0-.L1$pb)
// e.g. (gcc)
//    call _x86.get_pc_thunk.REG
//    add REG, _GLOBAL_OFFSET_TABLE_
reg_has_got(EA,Reg):-
    value_reg(EA,Reg,_,"NONE",_,Offset,_),
    got_section(Name),
    loaded_section(as(Offset,address),_,Name).

/**
Operand instruction at 'EA' at index 'Index' is a GOT-relative reference to an
address 'Dest'.
*/
.decl got_relative_operand(EA:address,Index:operand_index,Dest:address)
.output got_relative_operand

// GOT-relative references consist of a load of the GOT pointer to a register:
//     call    __x86.get_pc_thunk.ax
//     add     eax, OFFSET FLAT:_GLOBAL_OFFSET_TABLE_
// Followed by a use of the register, such as:
//     lea     edx, .LC0@GOTOFF[eax]
//     movzx   eax, WORD PTR array@GOTOFF[ebx+eax*2]
//     movsx   eax, BYTE PTR array@GOTOFF[esi+ebx]
got_relative_operand(EA,Index,Dest):-
    reg_has_got(EA_base,Reg),
    def_used(EA_base,Reg,EA,Index),
    instruction_get_op(EA,Index,Op),
    (
      op_indirect(Op,"NONE",Reg,_,_,Offset,_);
      op_indirect(Op,"NONE",_,Reg,1,Offset,_)
    ),
    Offset != 0,
    got_reference_pointer(Got),
    Dest=Got+as(Offset,address).

got_relative_operand(EA3,Index,as(Dest,address)):-
    reg_has_got(EA1,Reg1),
    def_used(EA1,Reg1,EA2,_),
    arch.reg_reg_arithmetic_operation(EA2,Reg3,Reg1,Reg2,Mult,0),
    def_used(EA2,Reg3,EA3,_),
    op_immediate_and_reg(EA3,_,Reg3,Index,_),
    value_reg(EA3,Reg3,_,Reg2,Mult,Dest,_).

symbolic_operand_candidate(EA,Index,Dest,"data"):-
    got_relative_operand(EA,Index,Dest).

.decl get_pc_thunk(EA:address,Reg:register)
.output get_pc_thunk

// call    __x86.get_pc_thunk.REG
// ...
// __x86.get_pc_thunk.REG:
// mov     REG, DWORD PTR [esp]
// ret
get_pc_thunk(EA,Reg):-
    binary_isa("X86"),
    binary_format("ELF"),
    direct_call(_,EA),
    instruction(EA,Size,_,Operation,Op1,Op2,_,_,_,_),
    arch.move_operation(Operation),
    op_regdirect(Op2,Reg),
    op_indirect(Op1,"NONE","ESP","NONE",1,0,_),
    arch.return(EA+Size).

got_reference(Got_entry,Symbol):-
    loaded_section(Beg,End,".got"),
    Got_entry >= Beg,
    Got_entry < End,
    relocation(Got_entry,RelType,Symbol,_,_,_,_),
    RelType != "RELATIVE",
    RelType != "DTPMOD64",
    symbol(_,_,_,_,_,_,_,_,Symbol).

got_reference(Got_entry,Symbol):-
    loaded_section(Beg,End,".got"),
    Got_entry >= Beg,
    Got_entry < End,
    arch.pointer_size(Ptr),
    symbolic_expr(Got_entry,Ptr,Symbol,_).

// TLS relocations @TLSGD and @TLSLD allocate two contiguous entries in GOT:
// typedef struct
// {
//   unsigned long int ti_module;
//   unsigned long int ti_offset;
// } tls_index;
symbol_minus_symbol(Got_entry+8,8,Beg,Dest,1,0):-
    relocation(Got_entry,"DTPMOD64","",_,_,_,_),
    (
        loaded_section(Beg,_,".tbss");
        loaded_section(Beg,_,".tdata")
    ),
    // ti_offset is initialized for @TLSGD and zero for @TLSLD
    data_word(Got_entry+8,8,Offset),
    Dest = as(as(Beg,number)+Offset,address).

got_reference(Got_entry,DestName):-
    relocation(Got_entry,"DTPMOD64","",_,_,_,_),
    symbolic_expr_symbol_minus_symbol(Got_entry+8,8,_,DestName,_,_).



//////////////////////////////////////////////////////////////////////////
// find the main function even if it does not have a symbol
//
.decl start_function(ea:address)

start_function(EA):-
    binary_format("ELF"),
    function_symbol(EA,"_start").

start_function(Start_location):-
    binary_format("ELF"),
    !function_symbol(Start_location,"_start"),
    entry_point(Start_location).

main_function(EA):-
    binary_format("ELF"),
    function_symbol(EA,"main").

/**
 * Decide whether the main function should be inferred.
 */
.decl do_infer_main_function()

do_infer_main_function():-
    binary_format("ELF"),
    !function_symbol(_,"main").

/**
* Infer the location of the main function.
*/
.decl inferred_main_function(Main_location:address)

inferred_main_function(Main_location):-
    arch.inferred_main_in_reg(Main_load,Reg),
    def_used(EA_def,Reg,Main_load,_),
    value_reg(EA_def,Reg,_,"NONE",_,Offset,_),
    Main_location = as(Offset, address),
    code(Main_location).

inferred_main_function(Main):-
    arch.inferred_main_in_reg(EA,Reg),
    instruction_get_op(EA,_,Op),
    op_indirect_contains_reg(Op,Reg),
    symbolic_operand(EA,_,Dest,"data"),
    address_in_data(Dest,Main).

// Inform the use_def analysis that the register is used here as an address.
used(EA,Reg,0),
used_for_address(EA,Reg):-
    arch.inferred_main_in_reg(EA,Reg).

main_function(Main_location):-
    inferred_main_function(Main_location).

inferred_special_symbol(EA,"_start","GLOBAL","FUNC","Beg"):-
    !binary_type("REL"),
    !symbol(EA,_,"FUNC",_,_,_,_,_,_),
    start_function(EA).

inferred_special_symbol(EA,"__do_global_dtors_aux","LOCAL","FUNC","Beg"):-
    function_symbol(EA,"__do_global_dtors_aux").

inferred_special_symbol(EA,"__do_global_dtors_aux","LOCAL","FUNC","Beg"):-
    !function_symbol(EA,"__do_global_dtors_aux"),
    function_inference.function_entry(EA),
    // referenced from .fini_array
    address_in_data(FiniArray,EA),
    loaded_section(Beg,End,".fini_array"),
    FiniArray >= Beg,
    FiniArray < End,
    // compare a value in bss with 0 in the first block
    code_in_refined_block(EA_cmp,EA),
    instruction(EA_cmp,_,_,"CMP",Op1,_,0,0,_,_),
    op_immediate(Op1,0),
    pc_relative_operand(EA_cmp,2,DataRef),
    bss_section(SectionName),
    loaded_section(BegData,EndData,SectionName),
    DataRef >= BegData,
    DataRef <= EndData.

inferred_special_symbol(EA,"_DYNAMIC","LOCAL","NONE","Beg"):-
    binary_format("ELF"),
    !symbol(EA,_,_,_,_,_,_,_,"_DYNAMIC"),
    loaded_section(EA,_,".dynamic").

inferred_special_symbol(EA,"_GLOBAL_OFFSET_TABLE_","LOCAL","NONE","Beg"):-
    !symbol(_,_,_,_,_,_,_,_,"_GLOBAL_OFFSET_TABLE_"),
    got_reference_pointer(EA).

no_return_function("abort(@.*)?"):- binary_format("ELF").
no_return_function("_?exit(@.*)?"):- binary_format("ELF").
no_return_function("__chk_fail(@.*)?"):- binary_format("ELF").
no_return_function("__stack_chk_fail(@.*)?"):- binary_format("ELF").
no_return_function("__assert_fail(@.*)?"):- binary_format("ELF").
no_return_function("longjmp(@.*)?"):- binary_format("ELF").

////////////////////////////////////////////////////////////////////////////////////////////

relocation_adjustment(EA,as(Addend,number),"implicit-addend"):-
    binary_format("ELF"),
    relocation(EA,Type,_,_,_,_,"REL"),
    relocation_size(Type,Size),
    (
        Size = 8, data_byte(EA,Addend);
        Size != 8, data_word(EA,Size/8,Addend)
    ).

relocation_adjustment(EA+Offset,N,"distance-to-pc"):-
    (
        binary_isa("X86");
        binary_isa("X64")
    ),
    binary_type("REL"),
    code(EA),
    (
        instruction(EA,Size,_,_,_,_,_,_,_,Offset);
        instruction(EA,Size,_,_,_,_,_,_,Offset,_)
    ),
    relocation(EA+Offset,Type,Symbol,_,SymbolIndex,_,_),
    (
        Type = "PC32";
        Type = "PLT32"
    ),
    symbol(_,_,_,_,_,_,_,SymbolIndex,Symbol),
    // Adjust addend for trailing instruction bytes.
    N = as(Size-Offset,number), N > 0.

symbolic_expr_from_relocation(EA,Size/8,Symbol,Addend+Adjustment,TargetEA):-
    binary_format("ELF"),
    relocation(EA,Type,Symbol,Addend,SymbolIndex,_,_),
    relocation_size(Type,Size),
    (
        Type = "R32";
        Type = "R64";
        Type = "R32S"
    ),
    relocation_adjustment_total(EA,Adjustment),
    symbol(TargetEA,_,_,_,_,_,_,SymbolIndex,Symbol),
    Symbol != "".

// Relocation indirectly references a named symbol with an offset from a SECTION symbol.
symbolic_expr_from_relocation(EA,Size/8,Symbol,0,TargetEA):-
    binary_type("REL"),
    // Nameless relocation referencing a SECTION symbol.
    relocation(EA,Type,"",Addend,SymbolIndex,_,_),
    relocation_size(Type,Size),
    (
        Type = "R32";
        Type = "R64";
        Type = "R32S"
    ),
    relocation_adjustment_total(EA,Adjustment),
    symbol(_,_,"SECTION",_,_,SectionIndex,_,SymbolIndex,_),
    section(_,_,Begin,_,SectionIndex),
    // Addend is the offset of a symbol within the Section.
    TargetEA = as(as(Begin,number)+Addend+Adjustment,address),
    (
        symbol(TargetEA,_,_,_,_,_,_,_,Symbol);
        !symbol(TargetEA,_,_,_,_,_,_,_,_), Symbol = ""
    ).

symbolic_expr_from_relocation(EA,Size,Symbol,Addend,TargetEA):-
    binary_format("ELF"),
    arch.pointer_size(Size),
    relocation(EA,"GLOB_DAT",Symbol,Addend,SymbolIndex,_,_),
    symbol(TargetEA,_,_,_,_,_,_,SymbolIndex,Symbol).

symbolic_expr_from_relocation(EA,4,Symbol,Addend,TargetEA):-
    binary_type("REL"),
    relocation(EA,"TPOFF32",Symbol,Addend,_,_,_),
    symbol(TargetEA,_,_,_,_,_,_,_,Symbol).

symbolic_expr_from_relocation(EA,4,Symbol,Addend+Adjustment,TargetEA):-
    binary_type("REL"),
    relocation(EA,Type,Symbol,Addend,SymbolIndex,_,_),
    (
        Type = "PC32";
        Type = "PLT32"
    ),
    relocation_adjustment_total(EA,Adjustment),
    symbol(TargetEA,_,_,_,_,_,_,SymbolIndex,Symbol),
    Symbol != "".

////////////////////////////////////////////////////////////////////////////////////////////

.decl tls_segment(Start:address,End:address,Align:unsigned)

tls_segment(Start,End,Align):-
    Start = min EA:{ tls_section(Name), loaded_section(Addr,_,Name), EA=Addr },
    End = max EA:{ tls_section(Name), loaded_section(_,End,Name), EA=End },
    Align = max X:{ tls_section(Name), section(Name,_,_,X,_) }.

// Segment-based references to thread-local storage,
//   e.g.   mov     eax, DWORD PTR fs:i@tpoff
//    or    mov     eax, DWORD PTR fs:[-4]
symbolic_operand_candidate(EA,Index,as(Reference+Offset,address),"data"):-
    code(EA),
    instruction_get_op(EA,Index,Op),
    op_indirect(Op,"FS","NONE","NONE",_,Offset,_), Offset < 0,
    tls_segment(_,TlsEnd,Align),
    Reference = as(TlsEnd,number) + as(TlsEnd % max(Align,1),number).

//   e.g.   mov      rax, qword ptr fs:[0]
//          lea      rax, [rax + threadLocal@TPOFF]
symbolic_operand_candidate(EA_used,Index,as(Reference+Offset,address),"data"):-
    instruction_get_op(EA_def,_,Op_def),
    op_indirect(Op_def,"FS","NONE","NONE",_,0,_),
    def_used(EA_def,_,EA_used,Index),
    instruction_get_op(EA_used,Index,Op_used),
    op_indirect(Op_used,"NONE",_,_,_,Offset,_), Offset < 0,
    tls_segment(_,TlsEnd,Align),
    Reference = as(TlsEnd,number) + as(TlsEnd % max(Align,1),number).

////////////////////////////////////////////////////////////////////
// Static binaries

.decl ifunc_symbol(EA:address,Name:symbol)

// We need two symbols, a function symbol and a GNU_IFUNC symbol
// so we can create a IRELATIVE relocation
ifunc_symbol(DestAddr,NameFunc),
inferred_special_symbol(DestAddr,NameFunc,"GLOBAL","GNU_IFUNC","Beg"),
inferred_special_symbol(DestAddr,Name,"LOCAL","FUNC","Beg"):-
    relocation(_,"IRELATIVE","",Dest,_,_,_),
    DestAddr = as(Dest,address),
    !symbol(DestAddr,_,"GNU_IFUNC",_,_,_,_,_,_),
    NameFunc = cat("FUN_",to_string(Dest)),
    Name = cat(NameFunc,"_IFUNC").


// these symbols point to the begining and end of the iplt section
// that contaisn the IRELATIVE relocations

inferred_special_symbol(Beg,"__rela_iplt_start","GLOBAL","NONE","Beg"):-
    loaded_section(Beg,_,".rela.plt"),
    !symbol(_,_,_,_,_,_,_,_,"__rela_iplt_start").

inferred_special_symbol(End,"__rela_iplt_end","GLOBAL","NONE","End"):-
    loaded_section(_,End,".rela.plt"),
    !symbol(_,_,_,_,_,_,_,_,"__rela_iplt_end").

abi_intrinsic(Beg,"__rela_iplt_start"),
abi_intrinsic(End,"__rela_iplt_end"):-
    loaded_section(Beg,End,".rela.plt").



// Special loop pattern seen in `__run_exit_handlers`
// of libc ubuntu20
// The alignment for symbols introduced by the pprinter is causing crashes
//   mov RBX,OFFSET __start___libc_atexit
//   mov RAX,OFFSET __stop___libc_atexit
//   ...
//    sub RAX,1
//    sub RAX,RBX
//    shr RAX,3
//    lea R12,QWORD PTR [RBX+RAX*8+8]
//    ...
//    call QWORD PTR [RBX]
// __stop___libc_atexit has 16 alignment, which introduces a zero address
// in the list of pointer iterated by this code
// We rewrite the code above as:
//   mov RBX,OFFSET __start___libc_atexit
//   mov RAX,OFFSET __start___libc_atexit+ CONSTANT
moved_label_class(EAStop,ImmIndexStop,"libc atexit"),
moved_immediate_candidate(EAStop,ImmIndexStop,ImmediateStopAddr,ImmediateStartAddr):-
    mov_immediate_to_reg(EAStart,_,_,ImmediateStart),
    ImmediateStartAddr = as(ImmediateStart, address),
    defined_symbol(ImmediateStartAddr,_,_,_,_,_,_,_,"__start___libc_atexit"),
    next(EAStart,EAStop),
    mov_immediate_to_reg(EAStop,_,ImmIndexStop,ImmediateStop),
    ImmediateStopAddr = as(ImmediateStop, address),
    defined_symbol(ImmediateStopAddr,_,_,_,_,_,_,_,"__stop___libc_atexit").
